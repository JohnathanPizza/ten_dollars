; gets a process into a runnable state at the start of the program
>SERVICE_INIT_ROM
	; comes with proc A = program id to load
	LDX	PROC_ACTIVE_ID
	LDA.X	PROC_A
	ASL
	TAX
	LDA.X	ROM_PROGRAM_LENGTH
	SEC
	SBC.I	1
	LDA.X	ROM_PROGRAM_LENGTH 1
	SBC.I	0
	CLC
	ADC.I	3
	; needed pages
	STA.Z	VR7
	JSR	PAGE_TABLE_VACANT
	CMP.Z	VR7
	BGE	_SERVICE_INIT_ROM_CONT
	JMP	_SERVICE_INIT_ROM_PAGES
>_SERVICE_INIT_ROM_CONT
	DEC.Z	VR7
	DEC.Z	VR7
	; get data pages
	JSR	PAGE_TABLE_GET
	TAY
	LDA	PROC_ACTIVE_ID
	STA.Y	PAGE_TABLE_PROCID
	LDA.I	0X80
	STA.Y	PAGE_TABLE_HOME
	PHY
	JSR	PAGE_MAP_GET_BACK
	TAY
	PLA
	STA.Y	PAGE_MAP
	
	JSR	PAGE_TABLE_GET
	TAY
	LDA	PROC_ACTIVE_ID
	STA.Y	PAGE_TABLE_PROCID
	LDA.I	0X81
	STA.Y	PAGE_TABLE_HOME
	PHY
	JSR	PAGE_MAP_GET_BACK
	TAY
	PLA
	STA.Y	PAGE_MAP

	; get code pages
	LDA.Z	VR7
	JSR	PAGE_MAP_LARGEST
	STA.Z	VR7 1

	PHA
>_SERVICE_INIT_ROM_LOOP
	LDX.Z	VR7 1
	JSR	PAGE_MAP_GET_BACK
	TAY
	JSR	PAGE_MAP_SWAP
	JSR	PAGE_TABLE_GET
	TAY
	LDA	PROC_ACTIVE_ID
	STA.Y	PAGE_TABLE_PROCID
	LDA.Z	VR7 1
	STA.Y	PAGE_TABLE_HOME
	TYA
	LDY.Z	VR7 1
	STA.Y	PAGE_MAP
	INC.Z	VR7 1
	DEC.Z	VR7
	BNE	_SERVICE_INIT_ROM_LOOP
	
	; copy
	PLA ; map idx
	CLC
	ADC.I	PAGE_MAP_RAM_BASE
	STA.Z	VR1 1
	STZ.Z	VR1
	LDX	PROC_ACTIVE_ID
	LDA.X	PROC_A
	ASL
	TAX
	LDA.X	ROM_PROGRAM_ARRAY
	STA.Z	VR0
	LDA.X	ROM_PROGRAM_ARRAY 1
	STA.Z	VR0 1
	LDA.X	ROM_PROGRAM_LENGTH
	STA.Z	VR4
	LDA.X	ROM_PROGRAM_LENGTH 1
	STA.Z	VR4 1
	JSR	MEMCPY

	; adjust
	LDA.Z	VR0
	STA.Z	VR4
	LDA.Z	VR0 1
	STA.Z	VR4 1
	LDA.Z	VR1
	STA.Z	VR0
	LDA.Z	VR1 1
	STA.Z	VR0 1
	LDA.X	ROM_PROGRAM_ADJUST
	STA.Z	VR1
	LDA.X	ROM_PROGRAM_ADJUST 1
	STA.Z	VR1 1
	JSR	PROGRAM_ADJUST

	; calc ptroff
	LDA	PROC_ACTIVE_ID
	ASL
	TAY
	SEC
	LDA.Z	VR0
	SBC.Z	VR4
	STA.Y	PROC_PTROFF
	LDA.Z	VR0 1
	SBC.Z	VR4 1
	STA.Y	PROC_PTROFF 1

	; set basic sched priority and state
	LDA.Z	VR0
	STA.Y	PROC_PC
	LDA.Z	VR0 1
	STA.Y	PROC_PC 1
	LDY	PROC_ACTIVE_ID
	LDA.I	0XFF
	STA.Y	PROC_S
	LDA.I	0B00100000
	STA.Y	PROC_P
	LDA.I	2
	STA.Y	PROC_SCHEDULE_PRIORITY
	LDA.I	PROC_STATE_READY
	STA.Y	PROC_STATE
	LDA.I	0XFF
	STA.Y	PROC_ACTIVE_DAP_ID
	
	JMP	SERVICE_CYCLE_BEGIN
>_SERVICE_INIT_ROM_PAGES
	; give some kind of error here maybe
	JMP	SERVICE_CYCLE_BEGIN
