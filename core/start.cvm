; entrypoint in intitialization code when the computer is turned on or reset

@0X8000
>START
	; hardware initialization
	
	; setting stack pointer
	LDX.I	0XFF	; load stack pointer default value
	TXS		; set stack pointer to 0x1FF

	; turning on hardware modules
	STA	HW_ENABLE_ADDR HW_RAM1 HW_VIA	; enable RAM1 and VIA

	; setting register values in the VIA
	LDA.I	0B11000101		; initial value for the PCR
	STA	VIA_PCR			; update PCR
	STZ	VIA_READ_PORT_DIR	; set the reading port to all input
	STZ	VIA_WRITE_PORT		; preset the write port to output 0
	LDA.I	0XFF			; load value to set write port to all output
	STA	VIA_WRITE_PORT_DIR	; set write port to all output
	LDA.I	0B00000000		; load initial ACR value
	STA	VIA_ACR			; store into ACR
	STA	HW_ENABLE_ADDR HW_RAM1	; disable VIA since setup is done

	; initialize RAM variables

	; zero everything from 0X200 to 0XFFF
	STZ.Z	VR0
	LDA.I	0X2
	STA.Z	VR0 1
	STZ.Z	VR4
	LDA.I	0XE
	STA.Z	VR4 1
	STZ.Z	VR5
	JSR	MEMSET
	
	LDA.I	PAGE_TABLE_PROCID
	STA.Z	VR0
	LDA.I	PAGE_TABLE_PROCID > 8
	STA.Z	VR0 1
	LDA.I	PAGE_COUNT_TOTAL
	STA.Z	VR4
	STZ.Z	VR4 1
	LDA.I	0XFF
	STA.Z	VR5
	JSR	MEMSET

	LDA.I	PAGE_MAP
	STA.Z	VR0
	LDA.I	PAGE_MAP > 8
	STA.Z	VR0 1
	LDA.I	0XFF
	STA.Z	VR5
	JSR	MEMSET

	LDA.I	0XFF
	STA	PROC_ACTIVE_ID

	; init finished

	CLI

	LDA.I	ASCII_ESC
	JSR	PRINTC
	LDA.I	ASCII_AT
	JSR	PRINTC
	LDA.I	ASCII_EXCLAIMATION
	JSR	PRINTC
	JSR	PRINTC
	JSR	PRINTL

	LDA.I	0
	JSR	LOADROM
	JSR	LOADROM
	LDA.I	1
	JSR	LOADROM
	LDA.I	0
	JSR	LOADROM
	JSR	LOADROM

	JMP	SERVICE_CYCLE_BEGIN

; A program idx
>LOADROM
	PHY
	PHA
	JSR	PROC_GET
	TAY
	PLA
	PHA
	STA.Y	PROC_A
	LDA.I	PROC_STATE_INIT_ROM
	STA.Y	PROC_STATE
	LDA.I	1
	STA.Y	PROC_SCHEDULE_PRIORITY
	LDA.I	0
	STA.Y	PROC_SCHEDULE_COUNT
	PLA
	PLY
	RTS

>PRINTWAIT
	LDA	IO_WRITE_STATUS
	BNE	PRINTWAIT
	RTS
