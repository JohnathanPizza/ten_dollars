; vr11 new process id
; if 0xff then no space for one
; else that is the new process id DONT LOSE IT!
>PROC_NEW
	PHA
	PHX
	LDA.Z	VR0
	STA.Z	VR8
	LDA.Z	VR0 1
	STA.Z	VR8 1
	LDA.Z	VR4
	STA.Z	VR9

	LDA.I	PROC_ARRAY_ALIVE
	STA.Z	VR0
	LDA.I	PROC_ARRAY_ALIVE > 8
	STA.Z	VR0 1
	LDX.I	0
>_PROC_NEW_LOOP
	STX.Z	VR4
	JSR	BITARRAY_GET
	CMP.I	0
	BEQ	_PROC_NEW_FOUND
	INX
	CPX.I	15
	BNE	_PROC_NEW_LOOP
	LDA.I	0XFF
	STA.Z	VR11
	BRA	_PROC_NEW_RETURN
>_PROC_NEW_FOUND
	JSR	BITARRAY_SET
	STX.Z	VR11
>_PROC_NEW_RETURN
	LDA.Z	VR9
	STA.Z	VR4
	LDA.Z	VR8 1
	STA.Z	VR0 1
	LDA.Z	VR8
	STA.Z	VR0
	PLX
	PLA
	RTS

; vr0 initial pc value
; vr4 proc id
>PROC_INIT
	PHA
	PHX
	LDX.Z	VR4
	LDA.I	0B00100000
	STA.X	PROC_ARRAY_P
	LDA.I	0XFF
	STA.X	PROC_ARRAY_S
	TXA
	ASL
	TAX
	LDA.Z	VR0
	STA.X	PROC_ARRAY_PC
	LDA.Z	VR0 1
	STA.X	PROC_ARRAY_PC 1
	PLX
	PLA
	RTS

; vr4 proc id
>PROC_ENTER
	LDY.Z	VR4
	STY	PROC_ACTIVE_ID
	LDX.Y	PROC_ARRAY_S
	TXS
	LDX.Y	PROC_ARRAY_P
	PHX
	TYA
	ASL
	TAX
	PLP
	JMP.XP	PROC_ARRAY_PC

>PROC_UNPAUSE
	LDA	PROC_PAUSE_A
	LDY	PROC_PAUSE_Y
	LDX	PROC_PAUSE_S
	TXS
	LDX	PROC_PAUSE_P
	PHX
	LDX	PROC_PAUSE_X
	PLP
	JMP.P	PROC_PAUSE_PC
